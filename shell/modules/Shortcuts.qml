import QtQuick
import Quickshell
import Quickshell.Io
import Caelestia
import Caelestia.Config
import qs.components.misc
import qs.services
import qs.modules.nexus

Scope {
    id: root

    property bool launcherInterrupted
    readonly property bool hasFullscreen: Hypr.focusedWorkspace?.toplevels?.values?.some(t => (t?.lastIpcObject?.fullscreen ?? 0) > 1) ?? false

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "nexus"
        description: "Open nexus"
        onPressed: WindowFactory.create()
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "showall"
        description: "Toggle launcher, dashboard and osd"
        onPressed: {
            if (root.hasFullscreen)
                return;
            const v = Visibilities.getForActive();
            v.launcher = v.dashboard = v.osd = v.utilities = !(v.launcher || v.dashboard || v.osd || v.utilities);
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "dashboard"
        description: "Toggle dashboard"
        onPressed: {
            if (root.hasFullscreen)
                return;
            const visibilities = Visibilities.getForActive();
            visibilities.dashboard = !visibilities.dashboard;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "screenshot"
        key: "Meta+Shift+S; Print"
        description: "Toggle screenshot overlay"
        onPressed: {
            if (root.hasFullscreen)
                return;
            regionSelector.screenshot();
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "googleLens"
        key: "Meta+Shift+A"
        description: "Toggle Google Lens search"
        onPressed: {
            if (root.hasFullscreen)
                return;
            regionSelector.search();
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "screenRecording"
        key: "Meta+Ctrl+S"
        description: "Toggle screen recording"
        onPressed: {
            if (root.hasFullscreen)
                return;
            regionSelector.record();
        }
    }

    // qmllint disable unresolved-type
    // USING plasma-wallpaper-application plugin for now
    // CustomShortcut {
    //     // qmllint enable unresolved-type
    //     name: "lock"
    //     key: "Meta+L"
    //     description: "Lock the current session"
    //     onPressed: {
    //         if (root.hasFullscreen)
    //             return;
    //         Quickshell.execDetached(["caelestia", "shell", "lock", "lock"]);
    //     }
    // }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "session"
        key: "Ctrl+Alt+Delete"
        description: "Toggle session menu"
        onPressed: {
            if (root.hasFullscreen)
                return;
            const visibilities = Visibilities.getForActive();
            visibilities.session = !visibilities.session;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "launcher"
        key: "Meta+Space; Meta"
        description: "Toggle launcher"
        onPressed: root.launcherInterrupted = false
        onReleased: {
            if (!root.launcherInterrupted && !root.hasFullscreen) {
                const visibilities = Visibilities.getForActive();
                visibilities.launcher = !visibilities.launcher;
            }
            root.launcherInterrupted = false;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "launcherInterrupt"
        description: "Interrupt launcher keybind"
        onPressed: root.launcherInterrupted = true
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "sidebar"
        key: "Meta+B"
        description: "Toggle sidebar"
        onPressed: {
            if (root.hasFullscreen)
                return;
            const visibilities = Visibilities.getForActive();
            Visibilities.initialSidebarTab = "notifications";
            visibilities.sidebar = !visibilities.sidebar;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "aiAssistant"
        description: "Toggle AI Assistant"
        onPressed: {
            if (root.hasFullscreen)
                return;
            const visibilities = Visibilities.getForActive();
            Visibilities.initialSidebarTab = "ai";
            visibilities.sidebar = true;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "utilities"
        description: "Toggle utilities"
        onPressed: {
            if (root.hasFullscreen)
                return;
            const visibilities = Visibilities.getForActive();
            visibilities.utilities = !visibilities.utilities;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "emoji"
        key: "Meta+Shift+V"
        description: "Open emoji picker"
        onPressed: {
            if (root.hasFullscreen)
                return;
            Visibilities.launcherInitialSearch = `${GlobalConfig.launcher.actionPrefix}emoji `;
            const visibilities = Visibilities.getForActive();
            visibilities.launcher = true;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "clipboard"
        key: "Meta+V"
        description: "Open clipboard history"
        onPressed: {
            if (root.hasFullscreen)
                return;
            Visibilities.launcherInitialSearch = `${GlobalConfig.launcher.actionPrefix}clipboard `;
            const visibilities = Visibilities.getForActive();
            visibilities.launcher = true;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "windowSwitcher"
        description: "Open window switcher"
        onPressed: {
            if (root.hasFullscreen)
                return;
            Visibilities.launcherInitialSearch = `${GlobalConfig.launcher.actionPrefix}windows `;
            const visibilities = Visibilities.getForActive();
            visibilities.launcher = true;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "wallpaper"
        key: "Meta+Ctrl+T"
        description: "Open wallpaper picker"
        onPressed: {
            if (root.hasFullscreen)
                return;
            Visibilities.launcherInitialSearch = `${GlobalConfig.launcher.actionPrefix}wallpaper `;
            const visibilities = Visibilities.getForActive();
            visibilities.launcher = true;
        }
    }

    // qmllint disable unresolved-type
    CustomShortcut {
        // qmllint enable unresolved-type
        name: "keybinds"
        key: "Meta+/"
        description: "Open keybinds list"
        onPressed: {
            if (root.hasFullscreen)
                return;
            Visibilities.launcherInitialSearch = `${GlobalConfig.launcher.actionPrefix}keybinds `;
            const visibilities = Visibilities.getForActive();
            visibilities.launcher = true;
        }
    }


    CustomShortcut {
        name: "foot"
        description: "Launch Terminal"
        key: "Meta+Return"
        onPressed: Quickshell.execDetached(["kstart", "--", "foot"])
    }

    CustomShortcut {
        name: "firefox"
        description: "Launch Browser"
        key: "Meta+W"
        onPressed: Quickshell.execDetached(["kstart", "--", "firefox"])
    }

    CustomShortcut {
        name: "code"
        description: "Launch Editor"
        key: "Meta+C"
        onPressed: Quickshell.execDetached(["kstart", "--", "code"])
    }

    CustomShortcut {
        name: "github-desktop"
        description: "Launch GitHub Desktop"
        key: "Meta+G"
        onPressed: Quickshell.execDetached(["kstart", "--", "github-desktop"])
    }

    CustomShortcut {
        name: "nemo"
        description: "Launch File Manager"
        key: "Meta+Alt+E"
        onPressed: Quickshell.execDetached(["kstart", "--", "nemo"])
    }
    
    CustomShortcut {
        name: "kcolorpicker"
        description: "Color Picker"
        key: "Meta+Shift+C"
        onPressed: Quickshell.execDetached(["/bin/bash", "-c", "~/.local/bin/kcolorpicker -a"])
    }

    Instantiator {
        model: 10
        delegate: CustomShortcut {
            name: `workspace${index + 1}`
            description: `Switch to workspace ${index + 1}`
            key: `Meta+${(index + 1) === 10 ? 0 : (index + 1)}`
            onPressed: Quickshell.execDetached(["qdbus6", "org.kde.kglobalaccel", "/component/kwin", "org.kde.kglobalaccel.Component.invokeShortcut", `Switch to Desktop ${index + 1}`])
        }
    }


    IpcHandler {
        function toggle(drawer: string): void {
            if (list().split("\n").includes(drawer)) {
                if (root.hasFullscreen && ["launcher", "session", "dashboard"].includes(drawer))
                    return;
                const visibilities = Visibilities.getForActive();
                visibilities[drawer] = !visibilities[drawer];
            } else {
                console.warn(lc, `Drawer "${drawer}" does not exist`);
            }
        }

        function toggleTab(drawer: string, tab: string): void {
            if (list().split("\n").includes(drawer)) {
                if (root.hasFullscreen && ["launcher", "session", "dashboard"].includes(drawer))
                    return;
                if (drawer === "sidebar" && tab !== "") {
                    Visibilities.initialSidebarTab = tab;
                    const visibilities = Visibilities.getForActive();
                    visibilities.sidebar = true;
                    return;
                }
                const visibilities = Visibilities.getForActive();
                visibilities[drawer] = !visibilities[drawer];
            } else {
                console.warn(lc, `Drawer "${drawer}" does not exist`);
            }
        }

        function list(): string {
            const visibilities = Visibilities.getForActive();
            return Object.keys(visibilities).filter(k => typeof visibilities[k] === "boolean").join("\n");
        }

        target: "drawers"
    }

    IpcHandler {
        function open(): void {
            WindowFactory.create();
        }

        target: "nexus"
    }

    IpcHandler {
        function info(title: string, message: string, icon: string): void {
            Toaster.toast(title, message, icon, Toast.Info);
        }

        function success(title: string, message: string, icon: string): void {
            Toaster.toast(title, message, icon, Toast.Success);
        }

        function warn(title: string, message: string, icon: string): void {
            Toaster.toast(title, message, icon, Toast.Warning);
        }

        function error(title: string, message: string, icon: string): void {
            Toaster.toast(title, message, icon, Toast.Error);
        }

        target: "toaster"
    }

    IpcHandler {
        function action(name: string): void {
            Visibilities.launcherInitialSearch =
            `${GlobalConfig.launcher.actionPrefix}${name} `;

            const visibilities = Visibilities.getForActive();
            visibilities.launcher = true;
        }

        target: "launcher"
    }

    LoggingCategory {
        id: lc

        name: "caelestia.qml.shortcuts"
        defaultLogLevel: LoggingCategory.Info
    }
}
