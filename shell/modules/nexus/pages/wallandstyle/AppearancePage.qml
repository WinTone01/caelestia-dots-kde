pragma ComponentBehavior: Bound

import QtQuick
import QtCore
import QtQuick.Layouts
import Caelestia.Components
import Caelestia.Config
import qs.components
import qs.components.controls
import qs.services
import qs.modules.nexus.common

PageBase {
    id: root

    isSubPage: true
    title: qsTr("Appearance")

    ColumnLayout {
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.top: parent.top
        width: root.cappedWidth
        spacing: Tokens.spacing.large

        Item {
            Layout.fillWidth: true
            Layout.preferredHeight: Tokens.padding.large
        }

        ColumnLayout {
            Layout.fillWidth: true
            spacing: 0

            ToggleRow {
                Layout.fillWidth: true
                first: true
                text: qsTr("Bezel mode (Pitch black)")
                subtext: qsTr("Make the shell pitch black to blend with display bezels")
                checked: Config.appearance.pitchBlack
                onToggled: GlobalConfig.appearance.pitchBlack = checked
            }

            ToggleRow {
                Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
                Layout.fillWidth: true
                text: qsTr("Islands")
                subtext: qsTr("Everything appears as its own floating widget (Very Experimental)")
                checked: GlobalConfig.appearance.islands
                onToggled: GlobalConfig.appearance.islands = checked
            }

            ToggleRow {
                Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
                Layout.fillWidth: true
                text: qsTr("Transparency")
                subtext: qsTr("Enable transparency across the shell")
                checked: GlobalConfig.appearance.transparency.enabled
                onToggled: {
                    GlobalConfig.appearance.transparency.enabled = checked
                    if (!checked) {
                        GlobalConfig.appearance.blur = false
                    }
                }
            }

            SliderRow {
                Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
                label: qsTr("Base Transparency")
                valueLabel: Math.round(value * 100) + "%"
                value: GlobalConfig.appearance.transparency.base
                enabled: GlobalConfig.appearance.transparency.enabled
                onMoved: v => GlobalConfig.appearance.transparency.base = v
            }

            SliderRow {
                Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
                label: qsTr("Layers Transparency")
                valueLabel: Math.round(value * 100) + "%"
                value: GlobalConfig.appearance.transparency.layers
                enabled: GlobalConfig.appearance.transparency.enabled
                onMoved: v => GlobalConfig.appearance.transparency.layers = v
            }

            ToggleRow {
                Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
                Layout.fillWidth: true
                text: qsTr("Background Blur")
                subtext: qsTr("Apply blur to transparent window backgrounds")
                checked: GlobalConfig.appearance.blur
                enabled: GlobalConfig.appearance.transparency.enabled
                onToggled: {
                    GlobalConfig.appearance.blur = checked
                    if (GlobalConfig.appearance.transparency.enabled && checked) {
                        // Hack to force Quickshell blur region to update when enabling blur
                        GlobalConfig.appearance.transparency.enabled = false
                        blurHackTimer.start()
                    }
                }

                Timer {
                    id: blurHackTimer
                    interval: 50
                    onTriggered: GlobalConfig.appearance.transparency.enabled = true
                }
            }

            Settings {
                id: blurSettings
                category: "Blur"
                property int blurQuality: 20
            }

            StepperRow {
                Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
                label: qsTr("Blur Corner Quality")
                subtext: qsTr("Number of regions used to render blur corners")
                value: blurSettings.blurQuality
                enabled: GlobalConfig.appearance.transparency.enabled && GlobalConfig.appearance.blur
                from: 1
                to: 100
                stepSize: 1
                onMoved: v => blurSettings.blurQuality = Math.round(v)
            }

            ToggleRow {
                Layout.topMargin: Tokens.spacing.extraSmall / 2 - parent.spacing
                Layout.fillWidth: true
                last: true
                text: qsTr("Dark theme")
                checked: !Colours.light
                onToggled: Colours.setMode(checked ? "dark" : "light")
            }
        }
    }
}
